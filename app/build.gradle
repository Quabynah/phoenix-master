/*
 * Copyright (c) 2018. Property of Dennis Kwabena Bilson. No unauthorized duplication of this material should be made without prior permission from the developer
 */

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

// query git for the SHA, Tag and commit count. Use these to automate versioning.
final def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
final def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
final
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.pergasus"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 6
        versionName "0.6"
        archivesBaseName = "phoenix-android"
        resConfig "en"
        multiDexEnabled true
        final def filesAuthorityValue = applicationId + ".shareprovider"
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "GIT_TAG", "\"${gitTag}\""
        buildConfigField "String", "GIT_COMMIT_COUNT", "\"${gitCommitCount}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            versionNameSuffix '-beta'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
        check 'NewApi', 'InlinedApi'
        fatal 'NewApi', 'InlinedApi'
        enable 'UnusedResources'
        checkReleaseBuilds true
        textReport false
        textOutput 'stdout'
        htmlReport true
        xmlReport true
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //ANDROID SUPPORT LIBS
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-dynamic-animation:27.0.2'
    implementation "com.android.support:customtabs:27.0.2"
    implementation 'com.android.support:palette-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:multidex:1.0.2'

    //AUTHENTICATION & PAYMENT
    implementation 'com.google.android.gms:play-services-auth:11.8.0'

    //EXTERNAL
    implementation "com.github.bumptech.glide:glide:4.6.1"
    kapt "com.github.bumptech.glide:compiler:4.6.1"
    implementation "com.github.bumptech.glide:okhttp3-integration:4.6.1"
    implementation "com.github.bumptech.glide:recyclerview-integration:4.6.1"
    implementation "com.google.code.gson:gson:2.8.2"

    //FIREBASE
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }
    implementation "com.google.firebase:firebase-core:11.8.0"
    implementation "com.google.firebase:firebase-auth:11.8.0"
    implementation "com.google.firebase:firebase-storage:11.8.0"
    implementation "com.google.firebase:firebase-firestore:11.8.0"
    implementation "com.google.firebase:firebase-messaging:11.8.0"
    implementation 'com.firebaseui:firebase-ui-auth:3.2.1'
    implementation 'com.google.android.gms:play-services-auth:11.8.0'

    //HUBTEL
    implementation 'com.github.deluakin:hubtel-payment:v1.3'

    //GOOGLE MAPS AND LOCATIONS SERVICES
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation 'com.google.android.gms:play-services-location:11.8.0'
    implementation 'com.google.android.gms:play-services-places:11.8.0'

    //RETROFIT
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.3.0"
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    //MODULES
    implementation project(":bypass")

    //TIMBER & MATERIAL DIALOG
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.jakewharton.timber:timber:4.6.0'

    //UNIT TESTING
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
apply plugin: 'com.google.gms.google-services'
